buildscript {
  ext {
    springBootVersion = '1.2.5.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}" 
    classpath 'io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE'
   	
	// Asset pipeline modules set as build dependencies
	classpath 'com.craigburke.angular:angular-annotate-asset-pipeline:2.1.2'
	classpath 'com.craigburke:js-closure-wrap-asset-pipeline:1.1.0' 
	classpath 'com.craigburke.angular:angular-template-asset-pipeline:2.2.1'
  }
}

plugins {
  id 'com.bertramlabs.plugins.asset-pipeline' version '2.4.2'
  id 'com.moowork.node' version '0.10'
  id 'com.craigburke.bower-installer' version '1.3.3'
  id 'com.craigburke.jasmine' version '1.0.1'
  id 'groovy'
}

apply plugin: 'spring-boot' 
apply plugin: 'io.spring.dependency-management' 

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'org.codehaus.groovy:groovy'

  compile 'com.bertramlabs.plugins:asset-pipeline-spring-boot:2.4.2'

  // Asset pipeline modules set as runtime dependencies
  runtime 'com.craigburke.angular:angular-annotate-asset-pipeline:2.1.2'
  runtime 'com.craigburke:js-closure-wrap-asset-pipeline:1.1.0' 
  runtime 'com.craigburke.angular:angular-template-asset-pipeline:2.2.1'
  
  testCompile 'org.springframework.boot:spring-boot-starter-test'
}

bower {
  dependencies = [
    'angular': '1.4.x',
    'bootstrap': '3.3.x',
    'fontawesome': '4.3.x',
    'highcharts-release': '4.1.x',
    'highcharts-ng': '0.0.8'
  ]
	
  sources = [
    'bootstrap': ['dist/css/bootstrap.min.css'],
    'highcharts-release': ['highcharts.js', 'adapters/standalone-framework.js']
  ]
	
  excludes = ['jquery']
}

jasmine {
  files = [
    'bower/angular/angular.js',
    'bower/**/*.js',
    '**/*.module.js',
    '**/!(*.spec).js',
    '**/*.spec.js'
  ]
}

run.dependsOn 'bowerInstall'
assetCompile.dependsOn 'bowerInstall'
test.dependsOn 'jasmineRun'